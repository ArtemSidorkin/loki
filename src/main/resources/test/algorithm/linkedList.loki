List:

	Node value next:
		$value = value
		$next = next

	$__root = none
	$__size = 0

	$size: $__size

	$add value:
		nwNd = Node.new(value, none)
		if ($__root == none) $__root = nwNd
		else
			itrtrNd = $__root
			while(itrtrNd.next != none) itrtrNd = itrtrNd.next
			itrtrNd.next = nwNd
		$__size = $__size + 1

	$removeByIndex index:
		if (index >= 0 && index < $__size)
			if (index == 0) $__root = $__root.next
			else
				itrtrNd = $__root
				i = 0
				while(i < index - 1)
					itrtrNd = itrtrNd.next
					i = i + 1
				itrtrNd.next = itrtrNd.next.next
			$__size = $__size - 1

	$get index:
		itrtrNd = $__root
		i = 0
		while(i < index)
			itrtrNd = itrtrNd.next
			i = i + 1
		itrtrNd.value


list = List.new()

assert(list.size(), 0)

list.add(10)
list.add(20)

assert(list.size(), 2)
assert(list.get(0), 10)
assert(list.get(1), 20)

list.add(30)
list.removeByIndex(1)
assert(list.get(1), 30)

list.removeByIndex(0)
assert(list.get(0), 30)

assert(list.size(), 1)
